import org.jetbrains.changelog.Changelog

plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'io.github.goooler.shadow' version '8.1.7'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
    id 'org.jetbrains.changelog' version '2.+'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url "https://maven.nucleoid.xyz/" }
    maven { url "https://api.modrinth.com/maven" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.mikeprimm.com' }
    maven { url 'https://repo.bluecolored.de/releases' } // BlueMapAPI
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation include("me.lucko:fabric-permissions-api:${project.permission_api_version}") {
        exclude group: 'net.fabricmc.fabric-api'
    }
    modImplementation include("eu.pb4:placeholder-api:${project.placeholder_api_version}")
    modImplementation include("eu.pb4:player-data-api:${project.player_data_api_version}")
    modImplementation include("xyz.nucleoid:server-translations-api:${project.translations_version}")
    implementation shadow("org.spongepowered:configurate-hocon:${project.configurate_hocon_version}")

    // Mod compat
    modCompileOnly "maven.modrinth:styled-chat:${project.styled_chat_version}"
    compileOnly "de.bluecolored:bluemap-api:${project.bluemap_api_version}"
    compileOnly "us.dynmap:DynmapCoreAPI:${project.dynmap_api_version}"
    compileOnly("xyz.jpenilla:squaremap-api:${project.squaremap_api}")
}

shadowJar {
    minimize()
    archiveClassifier.set("shadow")
    configurations = [project.configurations.shadow]
    from sourceSets.main.output
}

remapJar {
    from zipTree(shadowJar.archiveFile) dependsOn shadowJar
}

publishMods {
    file = remapJar.archiveFile
    type = STABLE
    changelog = fetchChangelog()

    displayName = "Vanish ${version.get()}"
    modLoaders.add("fabric")
    modLoaders.add("quilt")


    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "676275"
        minecraftVersions.add(project.curseforge_minecraft_version)
    }
    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "UL4bJFDY"
        minecraftVersions.add(project.minecraft_version)
    }
    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = providers.environmentVariable("GITHUB_REPOSITORY").getOrElse("DrexHD/dryrun")
        commitish = providers.environmentVariable("GITHUB_REF_NAME").getOrElse("dryrun")
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(21)
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
    }
}

private String fetchChangelog() {
    def log = getChangelog.changelog.get()
    if (log.has(project.mod_version)) {
        return log.renderItem(
                log.get(project.mod_version).withHeader(false),
                Changelog.OutputType.MARKDOWN
        )
    } else {
        return ""
    }
}

//Mixin hotswap
afterEvaluate {
    loom.runs.configureEach {
        // https://fabricmc.net/wiki/tutorial:mixin_hotswaps
        vmArg "-javaagent:${ configurations.compileClasspath.find { it.name.contains("sponge-mixin") } }"
    }
}
